
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьЗаголовокФормы();
	ПолучитьДанныеКонтейнеров();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыполненныеРаботыСегодняНажатие(Элемент)
	
	СформироватьОтчетПоРаботам(ТекущаяДата(), ТекущаяДата());
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполненныеРаботыВчераНажатие(Элемент)
	
	СформироватьОтчетПоРаботам(ТекущаяДата()-86400, ТекущаяДата()-86400);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполненныеРаботыНеделяНажатие(Элемент)
	
	СформироватьОтчетПоРаботам(ТекущаяДата()-86400*6, ТекущаяДата());

КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетПоРаботам(НачПериода, КонПериода)
	
	КлючВарианта = "ОсвоениеОбъемовРабот";
	ОткрытьОтчет("Отчет.ОсвоениеОбъемовРабот.Форма.ФормаОтчета", , , , КлючВарианта, НачПериода, КонПериода);

КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиСобытийГруппыВыполненныеРаботы

&НаСервере
Процедура ПолучитьДанные_ВыполненныеРаботы()
	
	Шрифт      = ШрифтыСтиля.ШрифтЗаголовкаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветЗаголовкаИнформационнойПанели;

	ВыполненныеРаботы_Заголовок = Новый ФорматированнаяСтрока("Выполненные работы", Шрифт, ЦветТекста);
	
	Реквизиты = Новый Массив;
	Реквизиты.Добавить("ВыполненныеРаботы_Сегодня");
	Реквизиты.Добавить("ВыполненныеРаботы_Вчера");
	Реквизиты.Добавить("ВыполненныеРаботы_Неделя");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""Сегодня"" КАК Период,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВыполненныеРаботыОбороты.Работа) КАК КоличествоРабот
	|ИЗ
	|	РегистрНакопления.ВыполненныеРаботы.Обороты(НАЧАЛОПЕРИОДА(&Сегодня, ДЕНЬ), КОНЕЦПЕРИОДА(&Сегодня, ДЕНЬ), , ) КАК ВыполненныеРаботыОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Вчера"",
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВыполненныеРаботыОбороты.Работа)
	|ИЗ
	|	РегистрНакопления.ВыполненныеРаботы.Обороты(НАЧАЛОПЕРИОДА(&Вчера, ДЕНЬ), КОНЕЦПЕРИОДА(&Вчера, ДЕНЬ), , ) КАК ВыполненныеРаботыОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Неделя"",
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВыполненныеРаботыОбороты.Работа)
	|ИЗ
	|	РегистрНакопления.ВыполненныеРаботы.Обороты(НАЧАЛОПЕРИОДА(&Неделя, ДЕНЬ), КОНЕЦПЕРИОДА(&Сегодня, ДЕНЬ), , ) КАК ВыполненныеРаботыОбороты";
	
	Запрос.УстановитьПараметр("Сегодня"	, ТекущаяДата());
	Запрос.УстановитьПараметр("Вчера"	, ТекущаяДата()-86400);
	Запрос.УстановитьПараметр("Неделя"	, ТекущаяДата()-86400*6);
	
	Шрифт      = ШрифтыСтиля.ШрифтТекстаИнформационнойПанели;
	ЦветТекста = ЦветаСтиля.ЦветТекстаИнформационнойПанели;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЭтаФорма["ВыполненныеРаботы_"+Выборка.Период] = Новый ФорматированнаяСтрока(Строка(Выборка.КоличествоРабот), Шрифт, ЦветТекста); 
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	Период = ТекущаяДатаСеанса();
	
	ПредставлениеТекущейДаты = Формат(Период, "Л=ru_RU; ДФ='d MMMM, dddd'");
	
	Заголовок = СтрШаблон(НСтр("ru = 'Сегодня: %1'"), ПредставлениеТекущейДаты);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеКонтейнеров()
	
	ПолучитьДанные_ВыполненныеРаботы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(Отчет, ГруппировкаОтчета = Неопределено, Отборы = Неопределено, Показатели  = Неопределено, КлючВарианта = Неопределено, НачалоПериода = Неопределено, КонецПериода = Неопределено)
	
	ПользовательскиеНастройки = ПользовательскиеНастройкиДляРасшифровки(НачалоПериода, КонецПериода);
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	
	Если ГруппировкаОтчета <> Неопределено И ТипЗнч(ГруппировкаОтчета) = Тип("Массив") Тогда
		ДополнительныеСвойства.Вставить("Группировка", ГруппировкаОтчета);
	КонецЕсли;
	
	Если ТипЗнч(Отборы) = Тип("Массив") Тогда
		
		НастройкаОтбора = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		НастройкаОтбора.ИдентификаторПользовательскойНастройки = "Отбор";
		Для Каждого Отбор Из Отборы Цикл
			РасширениеПечати.ДобавитьОтбор(НастройкаОтбора, Отбор.Поле, Отбор.Значение, Отбор.ВидСравнения);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТипЗнч(Показатели) = Тип("Массив") Тогда
		
		Для Каждого Показатель Из Показатели Цикл
			ДополнительныеСвойства.Вставить(Показатель, Истина);
		КонецЦикла;
		
	КонецЕсли;
	
	Если КлючВарианта <> Неопределено Тогда
		ДополнительныеСвойства.Вставить("КлючВарианта", КлючВарианта);
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("РежимРасшифровки"         , Истина);
	ПараметрыОтчета.Вставить("ВидРасшифровки"           , 2);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии"	, Истина);

	ОткрытьФорму(Отчет, ПараметрыОтчета, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Функция ПользовательскиеНастройкиДляРасшифровки(НачалоПериода = Неопределено, КонецПериода = Неопределено)
	
	// Инициализация пользовательских настроек
	// Добавим в настройки все параметры которые могут использоваться в отчетах руководителю
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
	
	Если НачалоПериода <> Неопределено Тогда
		ДополнительныеСвойства.Вставить("НачПериода", НачалоПериода);
	КонецЕсли;
	Если КонецПериода <> Неопределено Тогда
		ДополнительныеСвойства.Вставить("КонПериода", КонецПериода);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("Период", Новый СтандартныйПериод(НачалоПериода, КонецПериода));
	Возврат ПользовательскиеНастройки;
	
КонецФункции

#КонецОбласти

