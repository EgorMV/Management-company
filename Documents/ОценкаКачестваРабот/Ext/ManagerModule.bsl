// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Оценка
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ФормаОценки";
	КомандаПечати.Представление = НСтр("ru = 'Форма оценки'");
	КомандаПечати.Порядок       = 10;
		
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ФормаОценки") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"ФормаОценки",
			"Форма оценки",
			ПечатьФормыОценки(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ОценкаКачестваРабот.ПФ_MXL_ФормаОценки");
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьФормыОценки(МассивОбъектов, ОбъектыПечати = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Макет = ПолучитьМакет("ПФ_MXL_ФормаОценки");
	Область_Шапка 				= Макет.ПолучитьОбласть("Шапка");
	Область_ТаблРаботыЗаголовок	= Макет.ПолучитьОбласть("ТаблРаботыЗаголовок");
	Область_ТаблРаботыШапка 	= Макет.ПолучитьОбласть("ТаблРаботыШапка");
	Область_ТаблРаботыСтрока 	= Макет.ПолучитьОбласть("ТаблРаботыСтрока");
	Область_ТаблЗамечанияЗаголовок = Макет.ПолучитьОбласть("ТаблЗамечанияЗаголовок");
	Область_ТаблЗамечанияШапка 	= Макет.ПолучитьОбласть("ТаблЗамечанияШапка");
	Область_ТаблЗамечанияСтрока = Макет.ПолучитьОбласть("ТаблЗамечанияСтрока");
	Область_Пробел 				= Макет.ПолучитьОбласть("Пробел");  
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб         = Истина;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОценкаВыполненияРабот_ФормаОценки";
	
	СписокРабот = ПолучитьМакет("СписокРабот");
	СписокЗамечаний = ПолучитьМакет("СписокЗамечаний");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОценкаКачестваРабот.Ссылка,
	|	ОценкаКачестваРабот.Дата,
	|	ОценкаКачестваРабот.Номер,
	|	ОценкаКачестваРабот.Автор.ФизическоеЛицо КАК Проверяющий,
	|	ОценкаКачестваРабот.Проект,
	|	ОценкаКачестваРабот.ИтоговаяОценка,
	|	ЕСТЬNULL(ОценкаКачестваРабот.Автор.ФизическоеЛицо.ЭлектронныйАдрес, ""нет эл.почты"") КАК ЭлектронныйАдрес
	|ИЗ
	|	Документ.ОценкаКачестваРабот КАК ОценкаКачестваРабот
	|ГДЕ
	|	ОценкаКачестваРабот.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = Результат[0].Выбрать();	
	СчСтраниц = 0;
	Пока ВыборкаШапка.Следующий() Цикл
		Если СчСтраниц>0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Область_Шапка.Параметры.Заполнить(ВыборкаШапка);
		Область_Шапка.Параметры.Заголовок = "Оценка выполнения работ "+ВыборкаШапка.Номер+" от "+Формат(ВыборкаШапка.Дата, "ДФ=dd.MM.yyyy");
		Область_Шапка.Параметры.ДанныеПроверяющего = ""+ВыборкаШапка.Проверяющий+" ("+ВыборкаШапка.ЭлектронныйАдрес+")";
		ТабличныйДокумент.Вывести(Область_Шапка);
		
		КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;		
		
		//Выводим работы
		Настройки = СписокРабот.НастройкиПоУмолчанию;  
		РасширениеПечати.УстановитьПараметр(Настройки, "Ссылка", ВыборкаШапка.Ссылка);
		РасширениеПечати.УстановитьПараметр(Настройки, "КонецПериода", ВыборкаШапка.Дата);
		КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
		РасширениеПечати.ВывестиСКДВТабличныйДокумент(СписокРабот, ТабличныйДокумент, КомпоновщикНастроек);
		
		//Выводим замечания
		Настройки = СписокЗамечаний.НастройкиПоУмолчанию;  
		РасширениеПечати.УстановитьПараметр(Настройки, "Ссылка", ВыборкаШапка.Ссылка);
		КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
		РасширениеПечати.ВывестиСКДВТабличныйДокумент(СписокЗамечаний, ТабличныйДокумент, КомпоновщикНастроек);
		
		СчСтраниц = СчСтраниц+1;
	КонецЦикла;	
	 
	Возврат ТабличныйДокумент;
	
КонецФункции






